load DQL.maude

mod SECRET-SHARING is
    pr DQL-SEMANTICS .

    var S : Scalar .

    --- defining initial states
    ops a b : -> Scalar .
    eq Pow(Abs(a), 2) .+ Pow(Abs(b), 2) = 1 .
    eq S .* Pow(Abs(a), 2) .+ S .* Pow(Abs(b), 2) = S .

    op initQState : -> Qubit .
    eq initQState = (q[0]: a . |0> + b . |1>) (q[1]: |0>) (q[2]: |0>) (q[3]: |0>) .
endm

mod SECRET-SHARING-CHECK is
    pr SECRET-SHARING .
    pr CLOSED-SUBSPACES .

    --- defining the subspaces for atomic formulas
    op prop : -> AtomicFormula [ctor] .
    eq V(prop) = { qubitAt(initQState, 0) } .

    var Q : Qubit .

    --- checking atomic formulas for Teleportation
    eq Q |= prop = qubitAt(Q, 3) in V(prop) [print "Q = " Q] .
endm

in debug.maude

search in SECRET-SHARING-CHECK : initQState |= [ H(1) ; CX(1, 2) ; CX(2, 3) ; CX(0, 1) ; H(0) ; H(2) ;
if P(1, |0>) then skip else X(3) fi ;
if P(0, |0>) then skip else Z(3) fi ;
if P(2, |0>) then skip else Z(3) fi
] prop =>! JS:JudgementSet such that JS:JudgementSet =/= emptyJS .
